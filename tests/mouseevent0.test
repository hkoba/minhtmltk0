#!/bin/sh
# -*- mode: tcl; coding: utf-8 -*-
# the next line restarts using tclsh \
    exec tclsh -encoding utf-8 "$0" ${1+"$@"}
    
package require tcltest
tcltest::configure -testdir [file normalize [file dirname [info script]]]
tcltest::configure -loadfile \
    [file join [tcltest::testsDirectory] constraints.tcl]
tcltest::configure {*}$argv
tcltest::loadTestedCommands

namespace import -force tcltest::*

package require struct::list

set P mouseevent0
set type ::minhtmltk

test $P-setup setup -body {
    deleteWindows
    winfo children .
} -result ""

test $P-init load -body {
    # set abs [file normalize [info script]]
    # source [file join [file dirname [file dirname $abs]] \
    # 		[file rootname [file tail $abs]].tcl]
    set abs [file normalize [info script]]
    source [file join [file dirname [file dirname $abs]] \
		minhtmltk0.tcl]
} -result $type

namespace import ::minhtmltk::utils::*

#========================================

test $P-create "" -body {
    $type .ht -debug yes
} -result .ht

#========================================
# 

pack .ht -fill both -expand yes

incr D
test $P-$D-ready-eager "ready (eager)" -body {
    
    set ::out ""
    bind .ht <<DocumentReady>> [list set ::out READY]

    .ht configure -emit-ready-immediately yes

    .ht parse -final {
	<h2>Hello!</h2>
    }
    # Without entering eventloop.

    set ::out
} -result READY

.ht Reset
test $P-$D-ready-lazy "ready (lazy)" -body {
    
    .ht configure -emit-ready-immediately no

    set ::out ""

    .ht parse -final {
	<h2>Hello!</h2>
    }
    
    # Enter eventloop.
    update idletask; # This is required (unless -emit-ready-immediately is yes)

    set ::out
} -result READY

.ht Reset; # Note: This resets [$ht on trigger]s too!
test $P-$D-on-ready "(global) on event command" -body {

    set ::out ""
    bind .ht <<DocumentReady>> [list lappend ::out We are]
    .ht on ready [list lappend ::out READY!!!!]
    # [bind] handler and [$ht on ] handler can coexist and called in this order.

    # puts stderr handlers=[.ht node event dump-handlers]
    # puts stderr ready=[dict get [.ht node event dump-handlers] "" ready]

    .ht parse -final {
	<h2>Hello!</h2>
    }
    
    update idletask;

    set ::out
} -result {We are READY!!!!}

#========================================

proc + {a b} {expr {$a + $b}}
proc avg {a b} {expr {($a + $b)/2}}
proc center bbox {
    lassign $bbox x1 y1 x2 y2
    list [avg $x1 $x2] [avg $y1 $y2]
}

proc nodeCenter node {
    # puts stderr invoking=$node,tag=[$node tag]
    lassign [center [.ht bbox $node]] cx cy
    minhtmltk::utils::adjust-coords-from [.ht html] .ht cx cy
    list $cx $cy
}

proc invokeHandlerFor {meth pos} {
    .ht $meth .ht {*}$pos
}

proc invokeClick node {
    set pos [nodeCenter $node]
    invokeHandlerFor Press   $pos
    invokeHandlerFor Release $pos
}

incr D
.ht Reset
test $P-$D-click-[incr I] "on h2 click" -body {
    
    bind .ht <<DocumentReady>> {}; # Make sure previous 'ready' is removed.

    .ht parse -final {
	<h2>Hello!</h2>
    }
    
    set ::out ""
    .ht node event on h2 click {
	set ::out "Clicked!"
    }
    
    #update idletask; # Is not needed

    invokeClick [.ht search h2]

    set ::out

} -result Clicked!

#========================================

incr D
test $P-$D-multiple-listeners-[incr I] "Multiple event lisnters for same event" -body {
    
    set ::out ""
    .ht node event clear h2 click
    .ht node event on    h2 click { lappend ::out foo }
    .ht node event on    h2 click { lappend ::out bar }
    
    #update idletask; # Is not needed

    invokeClick [.ht search h2]

    set ::out

} -result {foo bar}


incr D
test $P-$D-parameters-[incr I] "Visible parameters in event handler" -body {
    
    set ::out ""
    .ht node event clear h2 click
    .ht node event on h2 click {
	lappend ::out {*}[lsort [info vars *]]
    }
    
    #update idletask; # Is not needed

    invokeClick [.ht search h2]

    set ::out

} -result [lsort {self win selfns node this args}]

incr D
.ht Reset
test $P-$D-hover-[incr I] "h2:hover" -body {
    
    # XXX: Without style, dynamic set doesn't take effect!
    .ht parse -final {
	<style>h2:hover {background: red;}</style>
	<h2>Hello!</h2>
	<pre>foobar</pre>
    }
    
    set res []
    lappend res [list init [.ht search h2:hover]]

    invokeHandlerFor Motion [nodeCenter [.ht search h2]]

    update idletask;
    # â†‘This is required for [.ht search h2:hover]
    # but is not required for [[.ht search h2] dynamic conditions].

    lappend res [list hovered [expr {[.ht search h2:hover] ne ""}] \
		     [[.ht search h2] dynamic conditions]]

    invokeHandlerFor Motion [nodeCenter [.ht search pre]]

    update idletask;

    lappend res [list out [.ht search h2:hover]]

    set res

} -result [list [list init []] \
	       [list hovered 1 h2:hover] \
	       [list out []]]

incr D
.ht Reset
test $P-$D-active-[incr I] "h2:active" -body {
    
    # XXX: Without style, dynamic set doesn't take effect!
    .ht parse -final {
	<style>
	h2 {border: solid 2px blue;}
	h2:hover {background: gray50;}
	h2:active {color: red;}
	</style>
	<h2>Hello!</h2>
	<pre>foobar</pre>
    }
    
    set res []
    lappend res [list init [.ht search h2:active]]

    invokeHandlerFor Motion [nodeCenter [.ht search h2]]
    invokeHandlerFor Press  [nodeCenter [.ht search h2]]

    update idletask;

    lappend res [list active [expr {[.ht search h2:active] ne ""}] \
		     [lsort [[.ht search h2] dynamic conditions]]]

    invokeHandlerFor Release [nodeCenter [.ht search h2]]
    invokeHandlerFor Motion  [nodeCenter [.ht search pre]]

    update idletask;

    lappend res [list out [.ht search h2:active]]

    set res

} -result [list [list init []] \
	       [list active 1 {h2:active h2:hover}] \
	       [list out []]]

#========================================

incr D
.ht Reset
test $P-$D-labelclick-[incr I] "label click => checkbox/radio" -body {
    
    .ht parse -final {
	<form>
	<label id="lq1"><input type="radio" name="q1" value="1">radio!</label>
	<label id="lq2"><input type="checkbox" name="q2" value="2">checkbox!</label>
	</form>
    }
    
    # update idletask

    set form [.ht form get 0]

    set res {}
    lappend res [list before [$form get_all]]
    
    # puts stderr handlers=[.ht node event dump-handlers]
    foreach val {1 2} {
	set name q$val
	set id l$name
	set label [.ht search #$id]
	# puts stderr kids=[$label children]
	invokeClick [lindex [$label children] end]
    }

    lappend res [list after [$form get_all]]

} -result [list [list before []] [list after {q1 1 q2 2}]]

#========================================

incr D
.ht Reset

test $P-$D-labelclick-[incr I] "label click with tagged elem" -body {

    .ht parse -final {
        <form>
        <label><input type="checkbox" name="q" value="1">value 1</label>
        <label><input type="checkbox" name="q" value="2"><small>value 2</small></label>
        </form>
    }

    update idletask

    # XXX: TODO font size abstraction
    .ht Press   .ht.sw.html 52 23
    .ht Release .ht.sw.html 52 23

    .ht Press   .ht.sw.html 122 23
    .ht Release .ht.sw.html 123 23

    set form [.ht form get 0]
    $form get_all
} -result {q {1 2}}

#========================================

incr D
.ht Reset
test $P-$D-submit-[incr I] "(global) on submit" -body {
    
    .ht parse -final {
	<form name="nav">
	<input type="submit" id="go" name="go" value="Go!">
	<input type="submit" id="back" name="back" value="back">
	</form>
    }
    
    set ::out []
    .ht on submit {
	array set props $args
	# lappend ::out [list get_all [$props(form) get_all]]
	lappend ::out [list names [lsort [array names props]]]
	lappend ::out [list name $props(name)]
	lappend ::out [list tag [$node tag]]
	lappend ::out [list node-name [$node attr -default "" name]]
    }

    # update idletask

    set form [.ht form get 0]

    # puts stderr handlers=[.ht node event dump-handlers]
    foreach id {go back} {
	lappend ::out $id
	set input [.ht search #$id]
	if {$input eq ""} {
	    error "Can't find node for id=$id"
	}
	if {[.ht cget -use-tk-button]} {
	    [$input replace] invoke
	} else {
	    .ht node event trigger $input click
	}
    }

    set ::out

} -result [list \
	       go \
	       [list names [list form name]] \
	       [list name go] \
	       [list tag input] \
	       [list node-name go] \
	       back \
	       [list names [list form name]] \
	       [list name back] \
	       [list tag input] \
	       [list node-name back] \
	      ]


tcltest::cleanupTests
