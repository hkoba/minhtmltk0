#!/bin/sh
# -*- mode: tcl; coding: utf-8 -*-
# the next line restarts using tclsh \
    exec tclsh -encoding utf-8 "$0" ${1+"$@"}
    
package require tcltest
namespace import tcltest::test

package require struct::list

set P formstate1
set type ::minhtmltk::formstate

test $P-init load -body {
    set abs [file normalize [info script]]
    source [file join [file dirname [file dirname $abs]] \
		[file rootname [file tail $abs]].tcl]
} -result $type

proc iota1 {n} {
    struct::list mapfor v [struct::list iota $n] {expr {$v+1}}
}

#========================================
set t_nodes {}
set t_names {}
set t_pairs {}
set i 0

test $P-create "" -body {
    set form [$type %AUTO%]
    list [$form cget -name] [$form cget -action] [$form cget -node] \
        [$form names] [$form node serialize]
} -result [list "" "" "" "" ""]

#========================================
# Underlying [$form node *] intefaces

#----------------------------------------
# single
#
incr D
set T single
set name ss1
lappend t_names $name
foreach value [list 1 2 3] {

    test $P-$T-node-add-single-value=$value "\$form node add single name=$name value=$value" -body {
	set node node-$name-[incr i]
	$form node add single  \
	    $node [list name $name value $value]
    } -match regexp -result {^::minhtmltk::formstate::Snit_inst\d+::_S\d+$}

    lappend t_nodes $node
    lappend t_pairs $name $value

    test $P-$T-node-dict-get-[incr i] "\$form node dict get" -body {
	set res ""
	foreach k [list name value] {
	    lappend res [$form node dict get $node $k]
	}
	lappend res [$form name dict get $name is_array]
	set res
    } -result [list ss1 $value 0]
}

test $P-$T-name-[incr i] "" -body {
    set res {}
    lappend res [$form names]
    lappend res [$form choicelist $name]
    lappend res [$form node serialize $t_nodes]
    set res
} -result [list [list $name] [list 1 2 3] []]


#----------------------------------------
# multi

incr D
set T multi
set name mm1
lappend t_names $name
foreach value [list 1 2 3] {

    test $P-$T-node-add-multi-value=$value "\$form node add multi name=$name value=$value" -body {
	set node node-$name-[incr i]
	$form node add multi  \
	    $node [list name $name value $value]
    } -match regexp -result {^::minhtmltk::formstate::Snit_inst\d+::_M\d+\(\d+\)$}

    lappend t_nodes $node
    lappend t_pairs $name $value
    
    test $P-$T-node-dict-get-[incr i] "\$form node dict get" -body {
	set res ""
	foreach k [list name value] {
	    lappend res [$form node dict get $node $k]
	}
	lappend res [$form name dict get $name is_array]
	set res
    } -result [list $name $value 1]
}

test $P-$T-name-[incr i] "" -body {
    set res {}
    lappend res [$form names]
    lappend res [$form choicelist $name]
    lappend res [$form node serialize $t_nodes]
    set res
} -result [list $t_names [list 1 2 3] []]

test $P-$T-checkbox-set-clear-[incr i] "" -body {
    set res {}
    lappend res [$form multi get $name]
    lappend res [$form multi test_all $name 1 2 3]

    $form multi set $name [list 1 3]
    lappend res [$form multi get $name]
    lappend res [$form multi test_all $name 1 2 3]
    lappend res [$form multi test_all $name 1 3]

    $form multi unset $name 1
    lappend res [$form multi get $name]

    $form multi unset $name 3
    lappend res [$form multi get $name]

    set res
} -result [list {*}{
    {}
    0
    {1 3}
    0
    1
    3
    {}
}]

$form multi unset $name {1 2 3}

test $P-$T-checkbox-set-clear-[incr i] "" -body {
    $form multi change $name 2 yes
    $form multi get $name
} -result 2

$form multi unset $name {1 2 3}

#----------------------------------------
# text

incr D
set T text
set name tt1
lappend t_names $name
foreach value [list foo bar baz] {

    test $P-$T-node-add-text-value=$value "\$form node add text name=$name value=$value" -body {
	set node node-$name-[incr i]
	$form node add text  \
	    $node [list name $name value $value]
    } -match regexp -result {^::minhtmltk::formstate::Snit_inst\d+::_T\d+$}

    lappend t_nodes $node
    lappend t_pairs $name $value
    
    test $P-$T-node-dict-get-[incr i] "\$form node dict get" -body {
	set res ""
	foreach k [list name value] {
	    lappend res [$form node dict get $node $k]
	}
	lappend res [$form name dict get $name is_array]
	set res
    } -result [list $name $value 0]
}

test $P-$T-name-[incr i] "" -body {
    set res {}
    lappend res [$form names]
    lappend res [$form choicelist $name]
    lappend res [$form node serialize $t_nodes]
    set res
} -result [list $t_names [] [list $name foo $name bar $name baz]]

#========================================

test $P-namedvars-n-serialize-[incr i] "" -body {
    set [lindex [$form namedvars ss1] 0] 2
    set [lindex [$form namedvars mm1] 0] 1
    set [lindex [$form namedvars tt1] 0] FOO
    $form node serialize $t_nodes
} -result [list ss1 2 mm1 [list 1] \
	       $name FOO $name bar $name baz]

test $P-namedvars-n-serialize-[incr i] "" -body {
    unset [lindex [$form namedvars ss1] 0]
    unset [lindex [$form namedvars mm1] 0]
    unset [lindex [$form namedvars tt1] 0]; # XXX: Unrealistic.
    $form node serialize $t_nodes
} -result [list $name bar $name baz]

#========================================

test $P-set-n-serialize-[incr i] "" -body {
    
    $form set ss1 3
    $form set mm1 [list 1 2]
    $form set tt1 BAR

    $form node serialize $t_nodes
} -result [list ss1 3 \
	       mm1 1 \
	       mm1 2 \
	       $name BAR $name bar $name baz]

test $P-set-n-get-[incr i] "" -body {
    
    list \
	[list ss1 [$form get ss1]] \
	[list mm1 [$form get mm1]] \
	[list tt1 [$form get tt1]]

} -result [list [list ss1 3] \
	       [list mm1 [list 1 2]] \
	       [list $name BAR] \
	       ]
