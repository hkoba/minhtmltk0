#!/bin/sh
# -*- mode: tcl; coding: utf-8 -*-
# the next line restarts using tclsh \
exec tclsh -encoding utf-8 "$0" ${1+"$@"}

package require tcltest
tcltest::configure -testdir [file normalize [file dirname [info script]]]
tcltest::configure -loadfile \
    [file join [tcltest::testsDirectory] constraints.tcl]
tcltest::configure {*}$argv
tcltest::loadTestedCommands

namespace import -force tcltest::*

#========================================

set P node-event
set type ::minhtmltk

test $P-setup setup -body {
    deleteWindows
    winfo children .
} -result ""

test $P-init load -body {
    # set abs [file normalize [info script]]
    # source [file join [file dirname [file dirname $abs]] \
    # 		[file rootname [file tail $abs]].tcl]
    set abs [file normalize [info script]]
    source [file join [file dirname [file dirname $abs]] \
		minhtmltk0.tcl]
} -result $type

namespace import ::minhtmltk::utils::*

#========================================

test $P-create "" -body {
    if {[winfo exists .ht]} { rename .ht "" }

    $type .ht -debug [::minhtmltk::utils::default \
                          ::env(DEBUG_HTMLTK_NODE_EVENT) 0]
} -result .ht

#========================================

pack .ht -fill both -expand yes
# .ht configure -emit-ready-immediately yes

incr D
test $P-$D-input-text "input text onchange" -body {

    set ::OUT ""

    .ht parse -final {
        <form>
        <input type="text" name="q1" onchange="lappend ::OUT foo">
        </form>
    }

    # Directly insert "bar" to q1
    [[.ht search input] replace] insert 0 bar

    list $::OUT [[.ht form get 0] get_all]

} -result {foo {q1 bar}}

incr D
test $P-$D-input-checkbox-radio "input checkbox, radio onchange" -body {
    .ht Reset

    set ::OUT ""

    .ht parse -final {
        <form>
        <input type="checkbox" name="q1" value="x" onchange="lappend ::OUT foo">
        <input type="radio"    name="q2" value="y" onchange="lappend ::OUT bar">
        </form>
    }

    # Directly invoke q1, q2
    [[.ht search {input[name=q1]}] replace] invoke
    [[.ht search {input[name=q2]}] replace] invoke

    list $::OUT [[.ht form get 0] get_all]

} -result {{foo bar} {q1 x q2 y}}

incr D
test $P-$D-select "select onchange" -body {
    .ht Reset

    set ::OUT ""

    .ht parse -final {
        <form>
        <select name="q1" onchange="lappend ::OUT foo">
        <option value="">↓Please select
        <option value="apple">I like apples
        <option value="orange">I like oranges
        </select>
        </form>
    }

    # Directly invoke q1
    [[[.ht search {select}] replace] cget -menu] invoke 1

    list $::OUT [[.ht form get 0] get_all]

} -result {foo {q1 apple}}

incr D
test $P-$D-cyclic-change "programmatical form changes should not invoke onchange" -body {
    .ht Reset

    proc onchange {self node name} {
        # puts [list onchange on node $node tag [$node tag] {*}[$node attr]]
        set form [$self form get 0]
        $form set $name [expr {1+[$form get $name]}]
    }

    .ht parse -final {
        <form>
        <input type="checkbox" name="q1" value="a" onchange="onchange $self $node counter">
        <input type="radio"    name="q2" value="b" onchange="onchange $self $node counter">
        <input type="text"     name="q3" value="c" onchange="onchange $self $node counter">
        <select name="q4" onchange="onchange $self $node counter">
        <option value="">↓Please select
        <option value="apple">I like apples
        <option value="orange">I like oranges
        </select>

        <input type="text"     name="counter" value=0 onchange="onchange $self $node counter">
        </form>
    }

    # Directly invoke q1, q2
    [[.ht search {input[name=q1]}] replace] invoke
    [[.ht search {input[name=q2]}] replace] invoke

    # Insert a character C via event generate. 
    set entry [[.ht search {input[name=q3]}] replace]
    $entry icursor end
    focus -force $entry; update; # needed so [event generate] delivers KeyPress. (taken from tests/ttk/treetags.test)
    event generate $entry <KeyPress-C>

    # Directly choose selectbox entry 1
    [[[.ht search {select[name=q4]}] replace] cget -menu] invoke 1

    [.ht form get 0] get_all

    # onchange should be called exactly 4 times.
} -result {q1 a q2 b q3 cC q4 apple counter 4}

tcltest::cleanupTests
